AC_INIT(bas.c)
AC_CONFIG_HEADERS(config.h)
AC_CANONICAL_HOST
VERSION=2.6
UPDATED='October 8, 2022'

ALL_LINGUAS="de"

LIBS="$LIBS -lm"

case $host in
  *-linux-*)
    if test "$prefix" = NONE
    then
      prefix=/usr
      AC_MSG_RESULT([using prefix $prefix])
    fi
  ;;
  m68k-cbm-sysv4)
    EXTRA_CFLAGS='-I/usr/local/include -I/usr/ucbinclude -I/usr/local/include/ncurses'
    EXTRA_LDFLAGS='-L/usr/ucblib -lucb'
  ;;
  hppa1.1-hp-hpux11.00)
    EXTRA_CFLAGS='+O2 +Onolimit -D_XOPEN_SOURCE_EXTENDED '
    EXTRA_LDFLAGS='-z '
    EXTRA_GCFLAGS='-D_XOPEN_SOURCE_EXTENDED '
    EXTRA_GLDFLAGS='-Wl,-z '
  ;;
  *-cygwin)
    AC_DEFINE(USE_SELECT)
  ;;
  *)
    prefix=$ac_default_prefix
  ;;
esac

AC_PROG_CC
if test "$GCC" = yes
then
  CFLAGS="${CFLAGS} ${EXTRA_GCFLAGS}-Wall -Wshadow -Wunused -Wbad-function-cast -Wmissing-prototypes -Wstrict-prototypes -Wcast-align -Wcast-qual -Wpointer-arith -Wwrite-strings -Wmissing-declarations -Wnested-externs -pedantic -fno-common"
  LDFLAGS="${LDFLAGS} ${EXTRA_GLDFLAGS}-g"

  ac_save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS -pipe"
  AC_MSG_CHECKING(if gcc accepts -pipe)
  AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[]],[[return 0]])],AC_MSG_RESULT(yes),CFLAGS="$ac_save_CFLAGS"; AC_MSG_RESULT(no))

  ac_save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS -Wextra"
  AC_MSG_CHECKING(if gcc accepts -Wextra)
  AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[]],[[return 0]])],AC_MSG_RESULT(yes),CFLAGS="$ac_save_CFLAGS"; AC_MSG_RESULT(no))

  ac_save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS -Wunused-result"
  AC_MSG_CHECKING(if gcc accepts -Wunused-result)
  AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[]],[[return 0]])],AC_MSG_RESULT(yes),CFLAGS="$ac_save_CFLAGS"; AC_MSG_RESULT(no))

  ac_save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS -Wno-unused-parameter"
  AC_MSG_CHECKING(if gcc accepts -Wno-unused-parameter)
  AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[]],[[return 0]])],AC_MSG_RESULT(yes),CFLAGS="$ac_save_CFLAGS"; AC_MSG_RESULT(no))

  ac_save_CFLAGS="$CFLAGS"
  CFLAGS="$CFLAGS -Wundef"
  AC_MSG_CHECKING(if gcc accepts -Wundef)
  AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[]],[[return 0]])],AC_MSG_RESULT(yes),CFLAGS="$ac_save_CFLAGS"; AC_MSG_RESULT(no))
else
  CFLAGS="${CFLAGS} ${EXTRA_CFLAGS}"
  LDFLAGS="${LDFLAGS} ${EXTRA_LDFLAGS}"
fi

AC_PROG_RANLIB

AC_PROG_LEX(noyywrap)
if test "x$LEX" != xflex; then
  AC_MSG_RESULT([no flex found, configure failed.])
  exit 1
fi

AC_CHECK_HEADERS(termcap.h,have_termcap_h=yes)
if test "$have_termcap_h" != yes; then
  AC_CHECK_HEADERS(curses.h,have_curses_h=yes)
fi

AC_MSG_CHECKING(for tputs return type)
AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[#ifdef HAVE_TERMCAP_H
#include <termcap.h>
#else
#include <curses.h>
#endif
static int outc(int c){ return c; }
]],[[return tputs((char*)0,0,outc);
]])],AC_MSG_RESULT(int),AC_DEFINE(TPUTS_RETURNS_VOID) AC_MSG_RESULT(void))

AC_CHECK_HEADERS(tgmath.h,have_tgmath_h=yes)

AC_CHECK_FUNCS(fmod,have_fmod=yes)
if test "$have_fmod" != yes; then
  AC_MSG_RESULT([no fmod found, configure failed.])
  exit 1
fi

AC_CHECK_FUNCS(lrint)
AC_CHECK_FUNCS(log2)
AC_CHECK_FUNCS(nanosleep)
AC_CHECK_FUNCS(gettimeofday)

AC_CHECK_FUNCS(tgetent,have_tgetent=yes)
if test "$have_tgetent" != yes; then
  AC_CHECK_LIB(curses, tgetent,AC_DEFINE(HAVE_TGETENT) have_tgetent=yes; LIBS="-lcurses $LIBS")
fi
if test "$have_tgetent" != yes; then
        AC_CHECK_LIB(termcap, tgetent, AC_DEFINE(HAVE_TGETENT) [have_tgetent=yes; LIBS="-ltermcap $LIBS"])
fi
# The following applies to sick Linux distributions.
if test "$have_tgetent" != yes; then
       AC_CHECK_LIB(ncurses, tgetent, AC_DEFINE(HAVE_TGETENT) [have_tgetent=yes; LIBS="-lncurses $LIBS"])
fi

AC_CHECK_TYPES([ssize_t])

AC_SEARCH_LIBS(gettimeofday, [ucb])

AC_ARG_WITH(dmalloc,
  [  --with-dmalloc=DIR      dmalloc include/library installation prefix],
  [CPPFLAGS="$CPPFLAGS -I$with_dmalloc/include"
  LDFLAGS="$LDFLAGS -L$with_dmalloc/lib"
  LIBS="$LIBS -ldmalloc"
  AC_DEFINE(USE_DMALLOC)]
)

AC_ARG_WITH(efence,
  [  --with-efence=DIR       efence include/library installation prefix],
  [CPPFLAGS="$CPPFLAGS -I$with_efence/include"
  LDFLAGS="$LDFLAGS -L$with_efence/lib"
  LIBS="$LIBS -lefence"]
)

AC_ARG_WITH(valgrind,
  [  --with-valgrind         run regression tests with valgrind],
  [VALGRIND="valgrind"
  AC_SUBST(VALGRIND)]
)

USE_NLS=no
AC_CHECK_PROG(MSGFMT,msgfmt,yes,no)
if test "$MSGFMT" = yes
then
  AC_CHECK_HEADERS(libintl.h,[LIBINTL=yes])
  if test "$LIBINTL" = yes
  then
    AC_SEARCH_LIBS(gettext,intl,[AC_DEFINE(HAVE_GETTEXT) USE_NLS=yes])
  fi
fi

AC_SYS_LARGEFILE

AC_PROG_INSTALL

AC_DEFINE_UNQUOTED(VERSION,"$VERSION")
AC_SUBST(VERSION)
AC_SUBST(UPDATED)
AC_SUBST(USE_NLS)
AC_CONFIG_FILES(Makefile bas.1 test/runbas)
AC_OUTPUT
chmod 755 test/runbas
